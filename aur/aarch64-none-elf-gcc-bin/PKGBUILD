# Maintainer: Valentin HÄƒloiu <vially.ichb@gmail.com>
# Contributor: Arti Zirk <arti.zirk@gmail.com>
# Contributor: 1Conan <me@1conan.com>
# Contributor: hendy643 <phenderson643@gmail.com>

_target=aarch64-none-elf
pkgname=${_target}-gcc-bin
pkgver=13.3.rel1
pkgrel=1
pkgdesc="The GNU Compiler Collection - cross compiler for ARM64 target"
arch=('aarch64')
url="https://developer.arm.com/downloads/-/arm-gnu-toolchain-downloads"
license=('GPL' 'LGPL')
source=(https://developer.arm.com/-/media/Files/downloads/gnu/${pkgver}/binrel/arm-gnu-toolchain-${pkgver}-aarch64-${_target}.tar.xz)
sha256sums=('fad7d567be5c095943d42f7078ea6f9a8452062dfe151152c2ec825814d254e0')
options=('!strip' '!debug')
provides=(aarch64-none-elf-gcc aarch64-none-elf-gcc-binutils aarch64-none-elf-gdb)
conflicts=(aarch64-none-elf-toolchain aarch64-none-elf-gcc aarch64-none-elf-gcc-binutils aarch64-none-elf-gdb)

package() {
  mkdir -p ${pkgdir}/usr/share/licenses/arm-gnu-toolchain
  cp -a ${srcdir}/arm-gnu-toolchain-${pkgver}-aarch64-${_target}/* ${pkgdir}/usr
  mv ${pkgdir}/usr/license.txt ${pkgdir}/usr/share/licenses/arm-gnu-toolchain/
  rm -f ${pkgdir}/usr/*-manifest.txt ${pkgdir}/usr/lib/bfd-plugins/libdep.so
  rm -rf ${pkgdir}/usr/include ${pkgdir}/usr/share/{dejagnu,doc,gcc-*,gdb,info,locale} ${pkgdir}/usr/share/man/{man1/runtest.1,man5,man7}
}
